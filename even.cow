#
# Copyright (c) 2020 Brian Callahan <bcallah@openbsd.org>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
#

# Pager
# Assumes ANSI terminal

include "cowgol.coh";
include "file.coh";
include "argv.coh";

const YMAX := 24;
const XMAX := 80;

const UP := 0;
const DOWN := 1;

var BufferStart: [uint8];
var BufferEnd: [uint8];
var GapStart: [uint8];

var AtLine: uint16 := 0;
var lines: uint16 := 0;
var c: uint8 := 0;

#
# Curses, kinda
#

# \033[
sub ConsoleControl() is
	print_char(0o033);
	print_char('[');
end sub;

sub ConsoleClear() is
	ConsoleControl();
	print("2J");
end sub;

sub ConsoleGoto(x: uint8, y: uint8) is
	if x == 0 and y == 0 then
		ConsoleControl();
		print_char('H');
	else
		var buffer: uint8[10];
		ConsoleControl();
		var pe: [uint8] := IToA((y as int32), 10, (&buffer as [uint8]));
		print(&buffer[0]);
		print_char(';');
		pe := IToA((x as int32), 10, (&buffer as [uint8]));
		print(&buffer[0]);
		print_char('H');
	end if;
end sub;

#
# Printing
#

sub LinePrint(): (done: uint8) is
	var x: uint8 := 0;

	while x < XMAX and c != 0 loop
		c := [GapStart];

		print_char(c);
		GapStart := GapStart + 1;

		if c == '\n' or c == 0 then
			break;
		end if;

		x := x + 1;
	end loop;

	while c != '\n' and c != 0 loop
		c := [GapStart];
		GapStart := GapStart + 1;
	end loop;

	if c == 0 then
		done := 1;
	else
		done := 0;
	end if;
end sub;

#
# Scrolling
#

sub GotoLine() is
	c := [GapStart];
	while c != '\n' and c != 0 loop
		GapStart := GapStart + 1;
		c := [GapStart];
	end loop;

	GapStart := GapStart + 1;
end sub;

sub Scroll(direction: uint8) is
	var line: uint16 := 0;
	var y: uint16 := 0;
	var done: uint8 := 0;

	if direction == DOWN then
		if AtLine == lines then
			return;
		end if;

		AtLine := AtLine + 1;
	else
		if AtLine == 0 then
			return;
		end if;

		AtLine := AtLine - 1;
	end if;

	GapStart := BufferStart;

	while line < AtLine loop
		GotoLine();
		line := line + 1;
	end loop;

	ConsoleClear();
	ConsoleGoto(0, 0);

	while y < YMAX and done == 0 loop
		done := LinePrint();
		y := y + 1;
	end loop;
end sub;

#
# File input
#

sub LoadFile(name: [uint8]) is
	var fcb: FCB;
	var len: uint32;

	if FCBOpenIn(&fcb, name) != 0 then
		ExitWithError();
	end if;

	len := FCBExt(&fcb);

	while len > 0 loop
		c := FCBGetChar(&fcb);

		if c < 0x20 or c > 0x7e then
			if c != 9 and c != 10 and c != 13 then
				break;
			end if;
		end if;

		if GapStart == BufferEnd then
			break;
		end if;

		if c == '\n' then
			lines := lines + 1;
		end if;

		[GapStart] := c;
		GapStart := GapStart + 1;

		len := len - 1;
	end loop;
	[GapStart] := 0;

	if FCBClose(&fcb) != 0 then
		ExitWithError();
	end if;
end sub;

#
# Main loop
#

ArgvInit();

var FileName: [uint8] := ArgvNext();

if FileName == (0 as [uint8]) then
	ExitWithError();
end if;

BufferStart := LOMEM;
BufferEnd := HIMEM - 1;
[BufferEnd] := 0;

GapStart := BufferStart;
LoadFile(FileName);
GapStart := BufferStart;

ConsoleClear();
ConsoleGoto(0, 0);

var done: uint8 := 0;
var y: uint8 := 0;
while y < YMAX and done == 0 loop
	done := LinePrint();
	y := y + 1;
end loop;

loop
	var ch: uint8 := get_char();

	case ch is
		when 'j': Scroll(DOWN);
		when 'k': Scroll(UP);
		when 'q': Exit();
	end case;
end loop;
